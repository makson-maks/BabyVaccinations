
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗаполнитьСоставляющиеВозраста();
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если Запись.ЕстьЗаписьКВрачу Тогда
		РаботаСУведомлениямиКлиент.СоздатьИДобавитьУведомление("Запись к врачу",
			"Вы записаны на " + Запись.ВремяЗаписи);
	КонецЕсли;
	
	ОбновитьПовторноИспользуемыеЗначения();
	Оповестить("ОбновитьФормуНачальнойСтраницы");
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Запись.ЕстьЗаписьКВрачу И Запись.ВремяЗаписи = НачалоДня(Запись.ВремяЗаписи) Тогда
		ТекстСообщения = НСтр("ru = 'Укажите время записи к врачу'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ВремяЗаписи", "Запись", Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработкаКомандФормы

&НаКлиенте
Процедура ПодтвердитьВакцинацию(Команда)
	
	ТекстВопроса = НСтр("ru = 'Вы подтверждаете, что вакцинация была выполнена %1'");
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстВопроса, Формат(Запись.ДатаВакцинации, "ДЛФ=DD"));
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПодтвердитьВакцинациюОтветНаВопрос", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса,РежимДиалогаВопрос.ДаНет, 60);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура РебенокПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Запись.ДатаВакцинации) Тогда
		Запись.ВозрастВакцинации = ПолучитьВозрастВакцинации(Запись.Ребенок, Запись.ДатаВакцинации);	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Запись.ВозрастВакцинации) Тогда
		РассчитатьДатуВакцинации(Запись.Ребенок, Запись.ВозрастВакцинации);	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВозрастВакцинацииПриИзменении(Элемент)
	
	РассчитатьДатуВакцинации(Запись.Ребенок, Запись.ВозрастВакцинации);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаВакцинацииПриИзменении(Элемент)
	
	Запись.ВозрастВакцинации = ПолучитьВозрастВакцинации(Запись.Ребенок, Запись.ДатаВакцинации);
	ЗаполнитьСоставляющиеВозраста();
	
	РассчитатьВремяЗаписиКВрачу();
	
КонецПроцедуры

&НаКлиенте
Процедура ВакцинаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Запись.Инфекция) Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Отбор = Новый Структура;
		Отбор.Вставить("Ссылка", ПолучитьДоступныеВакциныПоИнфекции(Запись.Инфекция));
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Отбор", Отбор);
		ПараметрыФормы.Вставить("Инфекция", Запись.Инфекция);
		
		ОткрытьФорму("Справочник.Вакцины.ФормаВыбора", ПараметрыФормы, Элемент);
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЛетПриИзменении(Элемент)
	
	РассчитатьВозрастДатуВакцинации();
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцевПриИзменении(Элемент)
	
	РассчитатьВозрастДатуВакцинации();
	
КонецПроцедуры

&НаКлиенте
Процедура НедельПриИзменении(Элемент)
	
	РассчитатьВозрастДатуВакцинации();
	
КонецПроцедуры

&НаКлиенте
Процедура ЕстьЗаписьКВрачуПриИзменении(Элемент)
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ВремяЗаписиПриИзменении(Элемент)
	
	РассчитатьВремяЗаписиКВрачу();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервереБезКонтекста
Функция ПолучитьДатуВакцинации(Ребенок, ВозрастВакцинации)
	
	ДатаРождения = ОбщегоНазначенияПовтИсп.ПолучитьДатуРожденияРебенка(Ребенок);
	
	Возврат ОбщегоНазначенияСервер.ПолучитьДатуВакцинацииПоДатеРождения(ДатаРождения, ВозрастВакцинации); 	
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьВозрастВакцинации(Ребенок, ДатаВакцинации)
	
	ДатаРождения = ОбщегоНазначенияПовтИсп.ПолучитьДатуРожденияРебенка(Ребенок);
	
	Возврат ОбщегоНазначенияСервер.ПолучитьВозрастВакцинацииПоДатеРождения(ДатаРождения, ДатаВакцинации); 	
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДоступныеВакциныПоИнфекции(Инфекция)
	
	Возврат ОбщегоНазначенияСервер.ПолучитьДоступныеВакциныПоИнфекции(Инфекция);
	
КонецФункции

&НаКлиенте
Процедура РассчитатьВозрастДатуВакцинации()
	
	Запись.ВозрастВакцинации = (Лет * 12) + Месяцев + (Недель / 10);
	
	РассчитатьДатуВакцинации(Запись.Ребенок, Запись.ВозрастВакцинации);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСоставляющиеВозраста()
	
	СоставляющиеВозраста = ОбщегоНазначенияКлиентСервер.РассчитатьСоставляющиеВозраста(Запись.ВозрастВакцинации);
	
	Лет 	= СоставляющиеВозраста.Лет;
	Месяцев = СоставляющиеВозраста.Месяцев;
	Недель  = СоставляющиеВозраста.Недель;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьДатуВакцинации(Ребенок, ВозрастВакцинации)
	
	Запись.ДатаВакцинации = ПолучитьДатуВакцинации(Ребенок, ВозрастВакцинации);
	
	РассчитатьВремяЗаписиКВрачу();
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьВремяЗаписиКВрачу()
	
	ВремяВакцинации = Формат(Запись.ВремяЗаписи, "ДФ='ЧЧ:мм'");
	Запись.ВремяЗаписи = Формат(Запись.ДатаВакцинации, "ДЛФ=D") + " " + ВремяВакцинации + ":00";

КонецПроцедуры

&НаКлиенте
Процедура УправлениеФормой()
	
	Элементы.ВремяЗаписи.Видимость = Запись.ЕстьЗаписьКВрачу;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодтвердитьВакцинациюОтветНаВопрос(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ПодтвердитьВакцинациюСервере();
		Оповестить("ОбновитьФормуНачальнойСтраницы");
		Если Открыта() Тогда
			Закрыть();
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПодтвердитьВакцинациюСервере()
	
	МенеджерЗаписи = РегистрыСведений.ВыполненныеВакцинации.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Запись);
	МенеджерЗаписи.Записать();
	
	МенеджерЗаписи = РегистрыСведений.ПрививкиВОчереди.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Запись);
	МенеджерЗаписи.Прочитать();
	МенеджерЗаписи.Удалить();
	
КонецПроцедуры

#КонецОбласти