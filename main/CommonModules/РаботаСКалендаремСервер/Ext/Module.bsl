
#Область ПрограммныйИнтерфейс

Процедура СформироватьКалендарьПрививок(РебенокСсылка) Экспорт
	
	ДатаРождения = РебенокСсылка.ДатаРождения;
	Календарь = РебенокСсылка.Календарь;
	ВходитВГруппуРиска = РебенокСсылка.ВходитВГруппуРиска;
	
	Если ВходитВГруппуРиска Тогда
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ИнфекцииМесяцаВакцинаций.Ссылка КАК Инфекция,
			|	ИнфекцииМесяцаВакцинаций.Месяц КАК Месяц
			|ИЗ
			|	Справочник.КалендариПрививок.СоставКалендаря КАК КалендариПрививокСоставКалендаря
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Инфекции.МесяцаВакцинаций КАК ИнфекцииМесяцаВакцинаций
			|		ПО КалендариПрививокСоставКалендаря.Инфекция = ИнфекцииМесяцаВакцинаций.Ссылка
			|ГДЕ
			|	КалендариПрививокСоставКалендаря.Ссылка = &Календарь
			|	И ИнфекцииМесяцаВакцинаций.Ссылка.НаличиеВГруппеРиска = ЗНАЧЕНИЕ(Перечисление.ТипыИнфекцияПоВхождениюВГруппыРиска.НеВходитВГруппуРиска)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ИнфекцииМесяцаВакцинацийДляЛицИзГруппРиска.Ссылка,
			|	ИнфекцииМесяцаВакцинацийДляЛицИзГруппРиска.Месяц
			|ИЗ
			|	Справочник.КалендариПрививок.СоставКалендаря КАК КалендариПрививокСоставКалендаря
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Инфекции.МесяцаВакцинацийДляЛицИзГруппРиска КАК ИнфекцииМесяцаВакцинацийДляЛицИзГруппРиска
			|		ПО КалендариПрививокСоставКалендаря.Инфекция = ИнфекцииМесяцаВакцинацийДляЛицИзГруппРиска.Ссылка
			|ГДЕ
			|	КалендариПрививокСоставКалендаря.Ссылка = &Календарь
			|	И ИнфекцииМесяцаВакцинацийДляЛицИзГруппРиска.Ссылка.НаличиеВГруппеРиска = ЗНАЧЕНИЕ(Перечисление.ТипыИнфекцияПоВхождениюВГруппыРиска.ЧастичноВходитВГруппуРиска)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ИнфекцииМесяцаВакцинацийДляЛицИзГруппРиска.Ссылка,
			|	ИнфекцииМесяцаВакцинацийДляЛицИзГруппРиска.Месяц
			|ИЗ
			|	Справочник.КалендариПрививок.СоставКалендаря КАК КалендариПрививокСоставКалендаря
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Инфекции.МесяцаВакцинацийДляЛицИзГруппРиска КАК ИнфекцииМесяцаВакцинацийДляЛицИзГруппРиска
			|		ПО КалендариПрививокСоставКалендаря.Инфекция = ИнфекцииМесяцаВакцинацийДляЛицИзГруппРиска.Ссылка
			|ГДЕ
			|	КалендариПрививокСоставКалендаря.Ссылка = &Календарь
			|	И ИнфекцииМесяцаВакцинацийДляЛицИзГруппРиска.Ссылка.НаличиеВГруппеРиска = ЗНАЧЕНИЕ(Перечисление.ТипыИнфекцияПоВхождениюВГруппыРиска.ВходитВГруппуРиска)";
	Иначе
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ИнфекцииМесяцаВакцинаций.Ссылка КАК Инфекция,
			|	ИнфекцииМесяцаВакцинаций.Месяц КАК Месяц
			|ИЗ
			|	Справочник.КалендариПрививок.СоставКалендаря КАК КалендариПрививокСоставКалендаря
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Инфекции.МесяцаВакцинаций КАК ИнфекцииМесяцаВакцинаций
			|		ПО КалендариПрививокСоставКалендаря.Инфекция = ИнфекцииМесяцаВакцинаций.Ссылка
			|ГДЕ
			|	КалендариПрививокСоставКалендаря.Ссылка = &Календарь
			|	И ИнфекцииМесяцаВакцинаций.Ссылка.НаличиеВГруппеРиска = ЗНАЧЕНИЕ(Перечисление.ТипыИнфекцияПоВхождениюВГруппыРиска.НеВходитВГруппуРиска)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ИнфекцииМесяцаВакцинаций.Ссылка,
			|	ИнфекцииМесяцаВакцинаций.Месяц
			|ИЗ
			|	Справочник.КалендариПрививок.СоставКалендаря КАК КалендариПрививокСоставКалендаря
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Инфекции.МесяцаВакцинаций КАК ИнфекцииМесяцаВакцинаций
			|		ПО КалендариПрививокСоставКалендаря.Инфекция = ИнфекцииМесяцаВакцинаций.Ссылка
			|ГДЕ
			|	КалендариПрививокСоставКалендаря.Ссылка = &Календарь
			|	И ИнфекцииМесяцаВакцинаций.Ссылка.НаличиеВГруппеРиска = ЗНАЧЕНИЕ(Перечисление.ТипыИнфекцияПоВхождениюВГруппыРиска.ЧастичноВходитВГруппуРиска)";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Календарь", Календарь);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаМесяцаВакцинаций = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаМесяцаВакцинаций.Следующий() Цикл
		
		Если Не ЗначениеЗаполнено(ВыборкаМесяцаВакцинаций.Инфекция) Тогда
			Продолжить;
		КонецЕсли;
		
		ДатаВакцинации = ОбщегоНазначенияСервер.ПолучитьДатуВакцинацииПоДатеРождения(ДатаРождения, ВыборкаМесяцаВакцинаций.Месяц);
		
		Если НачалоДня(ДатаВакцинации) < НачалоДня(ТекущаяДата()) Тогда
			Продолжить;
		КонецЕсли;
		
		МенеджерЗаписи = РегистрыСведений.ПрививкиВОчереди.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Ребенок = РебенокСсылка;
		МенеджерЗаписи.Инфекция = ВыборкаМесяцаВакцинаций.Инфекция;
		МенеджерЗаписи.ДатаВакцинации = ДатаВакцинации;
		МенеджерЗаписи.ВозрастВакцинации = ВыборкаМесяцаВакцинаций.Месяц;
		МенеджерЗаписи.Календарь = Календарь;
		МенеджерЗаписи.Записать();
		
	КонецЦикла;
	
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

Процедура СместитьКалендарьПрививок(Ребенок, ТекущаяЗапись, КоличествоДнейСмещения, ИсключитьИзСмещенияТекущуюЗапись) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПрививкиВОчереди.Ребенок КАК Ребенок,
		|	ПрививкиВОчереди.Инфекция КАК Инфекция,
		|	ПрививкиВОчереди.Вакцина КАК Вакцина,
		|	ПрививкиВОчереди.ДатаВакцинации КАК ДатаВакцинации
		|ИЗ
		|	РегистрСведений.ПрививкиВОчереди КАК ПрививкиВОчереди
		|ГДЕ
		|	ПрививкиВОчереди.Ребенок = &Ребенок";
	
	Запрос.УстановитьПараметр("Ребенок", Ребенок);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаЗапись = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаЗапись.Следующий() Цикл
		
		Если ИсключитьИзСмещенияТекущуюЗапись Тогда
			Если ВыборкаЗапись.Ребенок = ТекущаяЗапись.Ребенок
				И ВыборкаЗапись.Инфекция = ТекущаяЗапись.Инфекция
				И ВыборкаЗапись.Вакцина = ТекущаяЗапись.Вакцина 
				И ВыборкаЗапись.ДатаВакцинации = ТекущаяЗапись.ДатаВакцинации Тогда
				Продолжить;	
			КонецЕсли;
		КонецЕсли;
		
		МенеджерЗаписи = РегистрыСведений.ПрививкиВОчереди.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ВыборкаЗапись);
		МенеджерЗаписи.Прочитать();
		
		Если МенеджерЗаписи.Выбран() Тогда
			НоваяДатаВакцинации = ОбщегоНазначенияСервер.ДобавитьКДате(МенеджерЗаписи.ДатаВакцинации, КоличествоДнейСмещения, "ДЕНЬ");
			МенеджерЗаписи.ДатаВакцинации = НоваяДатаВакцинации;
			МенеджерЗаписи.ПлановаяДатаВакцинации = НоваяДатаВакцинации;
			МенеджерЗаписи.Записать();
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработатьУдалениеКалендаря(Календарь) Экспорт
	
	ОчиститьРеквизитКалендарьВсправочникеДети(Календарь);
	УдалитьПрививкиВОчередиПоКалендарю(Календарь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

Процедура ОчиститьРеквизитКалендарьВсправочникеДети(Календарь)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Дети.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Дети КАК Дети
		|ГДЕ
		|	Дети.Календарь = &Календарь";
	
	Запрос.УстановитьПараметр("Календарь", Календарь);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДети = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДети.Следующий() Цикл
		
		ДетиОбъект = ВыборкаДети.Ссылка.ПолучитьОбъект();
		ДетиОбъект.Календарь = Справочники.КалендариПрививок.ПустаяСсылка();
		ДетиОбъект.ОбменДанными.Загрузка = Истина;
		ДетиОбъект.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

Процедура УдалитьПрививкиВОчередиПоКалендарю(Календарь)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПрививкиВОчереди.Ребенок КАК Ребенок,
		|	ПрививкиВОчереди.Инфекция КАК Инфекция,
		|	ПрививкиВОчереди.Вакцина КАК Вакцина,
		|	ПрививкиВОчереди.ДатаВакцинации КАК ДатаВакцинации
		|ИЗ
		|	РегистрСведений.ПрививкиВОчереди КАК ПрививкиВОчереди
		|ГДЕ
		|	ПрививкиВОчереди.Календарь = &Календарь";
	
	Запрос.УстановитьПараметр("Календарь", Календарь);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаЗапись = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаЗапись.Следующий() Цикл
		
		МенеджерЗаписи = РегистрыСведений.ПрививкиВОчереди.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ВыборкаЗапись);
		МенеджерЗаписи.Прочитать();
		
		Если МенеджерЗаписи.Выбран() Тогда
			МенеджерЗаписи.Удалить();
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти