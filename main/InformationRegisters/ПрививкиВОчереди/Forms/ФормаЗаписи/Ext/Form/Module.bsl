
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ПодтвердитьВакцинацию.Видимость = Не Параметры.Ключ.Пустой();
	
	ДатаНапоминанияДоИзменений = Запись.ДатаНапоминания;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗаполнитьСоставляющиеВозраста();
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Запись.ЕстьЗаписьКВрачу И Запись.ВремяЗаписи = НачалоДня(Запись.ВремяЗаписи) Тогда
		ТекстСообщения = НСтр("ru = 'Укажите время записи к врачу'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ВремяЗаписи", "Запись", Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если Запись.ЕстьЗаписьКВрачу И Запись.ДатаНапоминания <> ДатаНапоминанияДоИзменений Тогда
		СтруктураДанных = ПолучитьСтруктуруДанныхРегистраДляКлючаЗаписи();
		СтруктураДанных.Вставить("ИмяРегистра", "ПрививкиВОчереди");
		РаботаСУведомлениямиКлиент.СоздатьИДобавитьУведомление("Запись к врачу",
			"Вы записаны на " + Запись.ВремяЗаписи, Запись.ДатаНапоминания, СтруктураДанных);
	КонецЕсли;
	
	ОбновитьПовторноИспользуемыеЗначения();
	Оповестить("ОбновитьФормуНачальнойСтраницы");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработкаКомандФормы

&НаКлиенте
Процедура ПодтвердитьВакцинацию(Команда)
	
	Если Параметры.Ключ.Пустой() Или Модифицированность Тогда
		Записать();
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru = 'Вы подтверждаете, что вакцинация была выполнена %1'");
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстВопроса, Формат(Запись.ДатаВакцинации, "ДЛФ=DD"));
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПодтвердитьВакцинациюОтветНаВопрос", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса,РежимДиалогаВопрос.ДаНет, 60);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьВакцинацию(Команда)
	
	Если Параметры.Ключ.Пустой() Или Модифицированность Тогда
		Записать();
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru = 'Отменить вакцинацию'");
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтменитьВакцинациюОтветНаВопрос", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса,РежимДиалогаВопрос.ДаНет, 60);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура РебенокПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Запись.ДатаВакцинации) Тогда
		Запись.ВозрастВакцинации = ПолучитьВозрастВакцинации(Запись.Ребенок, Запись.ДатаВакцинации);	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Запись.ВозрастВакцинации) Тогда
		РассчитатьДатуВакцинации(Запись.Ребенок, Запись.ВозрастВакцинации);	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВозрастВакцинацииПриИзменении(Элемент)
	
	РассчитатьДатуВакцинации(Запись.Ребенок, Запись.ВозрастВакцинации);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаВакцинацииПриИзменении(Элемент)
	
	Запись.ВозрастВакцинации = ПолучитьВозрастВакцинации(Запись.Ребенок, Запись.ДатаВакцинации);
	ЗаполнитьСоставляющиеВозраста();
	
	РассчитатьВремяЗаписиКВрачу();
	
КонецПроцедуры

&НаКлиенте
Процедура ВакцинаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Запись.Инфекция) Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Отбор = Новый Структура;
		Отбор.Вставить("Ссылка", ПолучитьДоступныеВакциныПоИнфекции(Запись.Инфекция));
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Отбор", Отбор);
		ПараметрыФормы.Вставить("Инфекция", Запись.Инфекция);
		
		ОткрытьФорму("Справочник.Вакцины.ФормаВыбора", ПараметрыФормы, Элемент);
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЛетПриИзменении(Элемент)
	
	РассчитатьВозрастДатуВакцинации();
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцевПриИзменении(Элемент)
	
	РассчитатьВозрастДатуВакцинации();
	
КонецПроцедуры

&НаКлиенте
Процедура НедельПриИзменении(Элемент)
	
	РассчитатьВозрастДатуВакцинации();
	
КонецПроцедуры

&НаКлиенте
Процедура ЕстьЗаписьКВрачуПриИзменении(Элемент)
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ВремяЗаписиПриИзменении(Элемент)
	
	РассчитатьВремяЗаписиКВрачу();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервереБезКонтекста
Функция ПолучитьДатуВакцинации(Ребенок, ВозрастВакцинации)
	
	ДатаРождения = ОбщегоНазначенияПовтИсп.ПолучитьДатуРожденияРебенка(Ребенок);
	
	Возврат ОбщегоНазначенияСервер.ПолучитьДатуВакцинацииПоДатеРождения(ДатаРождения, ВозрастВакцинации); 	
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьВозрастВакцинации(Ребенок, ДатаВакцинации)
	
	ДатаРождения = ОбщегоНазначенияПовтИсп.ПолучитьДатуРожденияРебенка(Ребенок);
	
	Возврат ОбщегоНазначенияСервер.ПолучитьВозрастВакцинацииПоДатеРождения(ДатаРождения, ДатаВакцинации); 	
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДоступныеВакциныПоИнфекции(Инфекция)
	
	Возврат ОбщегоНазначенияСервер.ПолучитьДоступныеВакциныПоИнфекции(Инфекция);
	
КонецФункции

&НаКлиенте
Процедура РассчитатьВозрастДатуВакцинации()
	
	Запись.ВозрастВакцинации = (Лет * 12) + Месяцев + (Недель / 10);
	
	РассчитатьДатуВакцинации(Запись.Ребенок, Запись.ВозрастВакцинации);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСоставляющиеВозраста()
	
	СоставляющиеВозраста = ОбщегоНазначенияКлиентСервер.РассчитатьСоставляющиеВозраста(Запись.ВозрастВакцинации);
	
	Лет 	= СоставляющиеВозраста.Лет;
	Месяцев = СоставляющиеВозраста.Месяцев;
	Недель  = СоставляющиеВозраста.Недель;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьДатуВакцинации(Ребенок, ВозрастВакцинации)
	
	Запись.ДатаВакцинации = ПолучитьДатуВакцинации(Ребенок, ВозрастВакцинации);
	
	РассчитатьВремяЗаписиКВрачу();
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьВремяЗаписиКВрачу()
	
	ВремяВакцинации = Формат(Запись.ВремяЗаписи, "ДФ='ЧЧ:мм'");
	Запись.ВремяЗаписи = Формат(Запись.ДатаВакцинации, "ДЛФ=D") + " " + ВремяВакцинации + ":00";

	РассчитатьДатуНапоминанияОДатеЗаписиКВрачу();
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеФормой()
	
	Элементы.ВремяЗаписи.Видимость = Запись.ЕстьЗаписьКВрачу;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодтвердитьВакцинациюОтветНаВопрос(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		ПодтвердитьВакцинациюСервере();
		
		КоличествоДнейСмещения = ОбщегоНазначенияСервер.РазностьДат(Запись.ПлановаяДатаВакцинации, Запись.ДатаВакцинации, "ДЕНЬ"); 
		
		Если КоличествоДнейСмещения <> 0 Тогда
			
			ТекстСообщения = НСтр("ru = 'По календарю вакцинация д.б. %1 Сместить календарь на %2 дн. %3?'");
			
			ТекстНаправленияСмещения = НСтр("ru = 'вперед'");
			Если КоличествоДнейСмещения < 0 Тогда
				ТекстНаправленияСмещения = НСтр("ru = 'назад'");
			КонецЕсли;
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения,
				Формат(Запись.ПлановаяДатаВакцинации, "ДЛФ=DD"),
				?(КоличествоДнейСмещения > 0, КоличествоДнейСмещения, КоличествоДнейСмещения * (-1)),
				ТекстНаправленияСмещения);
				
			ДополнительныеПараметры = Новый Структура;
			ДополнительныеПараметры.Вставить("КоличествоДнейСмещения", КоличествоДнейСмещения);
			
			ОписаниеОповещения = Новый ОписаниеОповещения("СмещениеКалендаряОтветНаВопрос", ЭтотОбъект, ДополнительныеПараметры);
			ПоказатьВопрос(ОписаниеОповещения, ТекстСообщения,РежимДиалогаВопрос.ДаНет, 60);
			
		Иначе
			
			ПриОкончанииПодтвержденияВакцинации();
			
		КонецЕсли;
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура СмещениеКалендаряОтветНаВопрос(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		СместитьКалендарьПрививокСервер(Запись.Ребенок, ДополнительныеПараметры.КоличествоДнейСмещения);	
	КонецЕсли; 
	
	ПриОкончанииПодтвержденияВакцинации();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОкончанииПодтвержденияВакцинации()
	
	Оповестить("ОбновитьФормуНачальнойСтраницы");
	Если Открыта() Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры
			
&НаСервере
Процедура ПодтвердитьВакцинациюСервере()
	
	МенеджерЗаписи = РегистрыСведений.ВыполненныеВакцинации.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Запись);
	МенеджерЗаписи.Записать();
	
	УдалитьЗаписьИзРегистраПрививкиВОчереди();
	
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

&НаСервере
Процедура УдалитьЗаписьИзРегистраПрививкиВОчереди()
	
	МенеджерЗаписи = РегистрыСведений.ПрививкиВОчереди.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Запись);
	МенеджерЗаписи.Прочитать();
	МенеджерЗаписи.Удалить();

КонецПроцедуры

&НаСервере
Функция ПолучитьСтруктуруДанныхРегистраДляКлючаЗаписи()
	
	СтруктураДанных = ОбщегоНазначенияСервер.СтруктураРегистраСведений("ПрививкиВОчереди");
	ЗаполнитьЗначенияСвойств(СтруктураДанных, ЭтотОбъект.Запись);
	
	Возврат СтруктураДанных;

КонецФункции

&НаКлиенте
Процедура РассчитатьДатуНапоминанияОДатеЗаписиКВрачу()
	
	Запись.ДатаНапоминания = Запись.ВремяЗаписи - (60 * 60 * 24);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СместитьКалендарьПрививокСервер(Ребенок, КоличествоДнейСмещения)

	РаботаСКалендаремСервер.СместитьКалендарьПрививок(Ребенок, КоличествоДнейСмещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьВакцинациюОтветНаВопрос(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		УдалитьЗаписьИзРегистраПрививкиВОчереди();
		ПриОкончанииПодтвержденияВакцинации();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти